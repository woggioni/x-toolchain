# Maintainer: Anatol Pomozov
# Contributor: Zhuoyun Wei <wzyboy@wzyboy.org>

pkgname="${_target}-libnghttp2"
_basename="${pkgname#${_target}-}"
_tag='a1d5296e5114eac3801f9c12a5f5a698ff97abdb' # git rev-parse v${pkgver}
pkgver=1.57.0
pkgrel=1
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=('x86_64')
url='https://nghttp2.org/'
license=('MIT')
options=('staticlibs' '!buildflags')
depends=("${_target}-gcc")
makedepends=('git' "${_target}-configure")
conflicts=("${_target}-nghttp2<1.20.0-2")
validpgpkeys=('F4F3B91474D1EB29889BD0EF7E8403D5D673C366') # Tatsuhiro Tsujikawa <tatsuhiro.t@gmail.com>
source=("git+https://github.com/nghttp2/nghttp2.git#tag=${_tag}?signed")
sha512sums=('SKIP')

prepare() {
  cd nghttp2

  autoreconf -i
}

build() {
  cd nghttp2

#   ${_target}-configure \
#     --disable-examples \
#     --disable-python-bindings \
#     --enable-lib-only
#   make
  mkdir -p build-${_target}-static && pushd build-${_target}-static
  ${_target}-configure $configure_args \
      --disable-examples --disable-python-bindings --enable-lib-only --enable-static --disable-shared ..
  make
  popd
  mkdir -p build-${_target} && pushd build-${_target}
  ${_target}-configure $configure_args \
      --disable-examples --disable-python-bindings --enable-lib-only --disable-static --enable-shared ..
  make
  popd
}


package() {
  cd "${srcdir}/nghttp2/build-${_target}-static/lib"
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}/nghttp2/build-${_target}/lib"
  make DESTDIR="${pkgdir}" install
}

strip() {
    ${_target}-strip $@
}
export -f strip