# Maintainer: Anatol Pomozov
# Contributor: Zhuoyun Wei <wzyboy@wzyboy.org>

pkgname="${_target}-libnghttp2"
_basename="${pkgname#${_target}-}"
_tag='a1d5296e5114eac3801f9c12a5f5a698ff97abdb' # git rev-parse v${pkgver}
pkgver=1.57.0
pkgrel=1
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=('x86_64')
url='https://nghttp2.org/'
license=('MIT')
options=('staticlibs' '!buildflags')
depends=("${_target}-gcc" "${_target}-openssl" "${_target}-libnghttp3" "${_target}-libngtcp2" "${_target}-libev" "${_target}-c-ares")
makedepends=('git' "${_target}-configure")
conflicts=("${_target}-nghttp2<1.20.0-2")
validpgpkeys=('F4F3B91474D1EB29889BD0EF7E8403D5D673C366') # Tatsuhiro Tsujikawa <tatsuhiro.t@gmail.com>
source=("git+https://github.com/nghttp2/nghttp2.git#tag=${_tag}?signed" '001-libressl.patch')
sha512sums=(
  'SKIP'
  '255725dd7d2accc7ac8dbf47847207a0c901712a3832fa9c97434c292b42db4d54a7f64450aea076fa4373e44c4e1318380b64e0dea7d838773f631d1d64292f'
)

prepare() {
  pushd nghttp2
    git apply ${srcdir}/001-libressl.patch
  popd
  find "nghttp2" -name '*.cc' -type f -exec \
          sed -i -e "s/NGTCP2_CC_ALGO_BBR_V2/NGTCP2_CC_ALGO_BBR/g" \
                 -e "s/-lharfbuzz /-lharfbuzz -lfreetype -lharfbuzz -liconv -lbz2 /g" {} \;
                 

  local cmake_options=(
    -DCMAKE_BUILD_TYPE=Release
    -DENABLE_APP=ON
    -DENABLE_DOC=OFF
    -DENABLE_HTTP3=ON
    -DENABLE_STATIC_LIB=ON
  )
  ${_target}-cmake -G Ninja ${cmake_options[@]} -S nghttp2 -B build
}

build() {
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  rm -r "${pkgdir}/${_prefix}/${_target}/sysroot/usr/share"
}

strip() {
    ${_target}-strip $@
}
export -f strip