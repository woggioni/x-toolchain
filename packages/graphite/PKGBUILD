
pkgname=${_target}-graphite
_basename=${pkgname#${_target}-}
pkgver=1.3.14
pkgrel=2
pkgdesc="Reimplementation of the SIL Graphite text processing engine (mingw-w64)"
arch=('x86_64')
url="https://github.com/silnrsi/graphite"
license=('LGPL' 'GPL' 'custom')
depends=("${_target}-gcc")
makedepends=("${_target}-cmake"
             'python' 'ninja')
options=('staticlibs' '!buildflags')
source=("https://github.com/silnrsi/graphite/releases/download/${pkgver}/graphite2-${pkgver}.tgz"
        "002-graphite2-1.2.1-pkgconfig.patch"
        "003-graphite2-1.3.8-staticbuild.patch")
sha256sums=('f99d1c13aa5fa296898a181dff9b82fb25f6cc0933dbaa7a475d8109bd54209d'
            '22bef0d90c0266b3b0eff01859c1e002f84c165d12242c2bfc153f8f98fb272a'
            'c139e0bed6e45f333fbdc136a5013395fe22ca5381eac2294958bc85ea1b69fd')

prepare() {
  cd graphite2-${pkgver}
  patch -p1 -i "${srcdir}"/002-graphite2-1.2.1-pkgconfig.patch
  patch -p1 -i "${srcdir}"/003-graphite2-1.3.8-staticbuild.patch
}

build() {
  mkdir "build" && pushd "build"
  ${_target}-cmake \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DGRAPHITE2_COMPARE_RENDERER=OFF \
    ${srcdir}/graphite2-${pkgver}
  cmake --build .
  popd
}

package() {
  cd "${srcdir}/build"
  DESTDIR="${pkgdir}" cmake --install . 
  mkdir -p ${pkgdir}/${_prefix}/${_target}/sysroot/usr 
  mv "${pkgdir}/${_prefix}/${_target}/sysroot/"{bin,include,lib,share} "${pkgdir}/${_prefix}/${_target}/sysroot/usr"
}

strip() {
    ${_target}-strip $@
}
export -f strip

# vim:set ts=2 sw=2 et:
