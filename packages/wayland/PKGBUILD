# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname="${_target}-wayland"
_basename="${pkgname#${_target}-}"
pkgver=1.22.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='https://wayland.freedesktop.org/'
license=('MIT')
options=('staticlibs' '!buildflags')
depends=("${_target}-configure" "${_target}-gcc" "${_target}-libffi" "${_target}-expat" "${_target}-libxml2")
makedepends=("${_target}-meson" "meson" 'ninja' "${_target}-libxslt" 'xmlto' 'graphviz' 'docbook-xsl')

validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611'  # Bryce Harrington
              'C0066D7DB8E9AC6844D728715E54498E697F11D7'  # Derek Foreman
              '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # emersion <contact@emersion.fr>
source=("https://gitlab.freedesktop.org/wayland/wayland/-/releases/$pkgver/downloads/wayland-$pkgver.tar.xz"{,.sig})
sha256sums=('1540af1ea698a471c2d8e9d288332c7e0fd360c8f1d12936ebb7e7cbc2425842'
            'SKIP')

build() {
  ${_target}-meson -Ddocumentation=false -Dtests=false -Ddefault_library=both -Dscanner=false $_basename-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

strip() {
    ${_target}-strip $@
}
export -f strip
