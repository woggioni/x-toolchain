# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>

pkgname="${_target}-libxml2"
_basename="${pkgname#${_target}-}"
pkgver=2.12.6
pkgrel=1
pkgdesc="XML parsing library, version 2"
url="http://www.xmlsoft.org/"
arch=(x86_64)
license=(MIT)
options=(staticlibs !buildflags)
depends=("${_target}-zlib" "${_target}-readline" "${_target}-ncurses" "${_target}-xz" "${_target}-icu")
makedepends=("${_target}-python" git "${_target}-configure")
_commit=505e2e872e767405be565aa9d47ae9d6d5677f79  # tags/v2.12.6^0
source=(
  "git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=$_commit"
  https://www.w3.org/XML/Test/xmlts20130923.tar.gz
)
b2sums=('SKIP'
        '63a47bc69278ef510cd0b3779aed729e1b309e30efa0015d28ed051cc03f9dfddb447ab57b07b3393e8f47393d15473b0e199c34cb1f5f746b15ddfaa55670be')


pkgver() {
  cd ${_basename}
  git describe --always --tags | sed 's/-rc/rc/;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir build
  cd ${_basename}  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd build
  ${_target}-configure \
    --with-threads \
    --with-history \
    --with-python=${_prefix}/${_target}/sysroot/usr/bin/python3 \
    --with-icu ../${_basename}/
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  PYTHONHASHSEED=0 make
  find doc -type f -exec chmod 0644 {} +
}

package() {
  make -C build DESTDIR="$pkgdir" install
  rm -r $pkgdir/${_prefix}/${_target}/sysroot/lib
  #rm -r $pkgdir/${_prefix}/${_target}/sysroot/usr/share/doc
}

strip() {
    ${_target}-strip $@
}
export -f strip