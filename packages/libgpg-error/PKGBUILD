# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname="${_target}-libgpg-error"
_basename="${pkgname#${_target}-}"
pkgver=1.51
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="https://www.gnupg.org"
license=('LGPL')
options=(staticlibs !buildflags)
depends=("${_target}-gcc" 'sh')
makedepends=("${_target}-configure")
source=(https://www.gnupg.org/ftp/gcrypt/libgpg-error/${_basename}-${pkgver}.tar.bz2{,.sig})
# https://www.gnupg.org/download/integrity_check.html
sha256sums=('be0f1b2db6b93eed55369cdf79f19f72750c8c7c39fc20b577e724545427e6b2'
            'SKIP')

validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # Werner Koch (dist signing 2020)
validpgpkeys+=('AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD') # Niibe Yutaka (GnuPG Release Key)

build() {
  cd ${_basename}-${pkgver}

  if [[ $_os == linux* ]]
  then
    if [[ $_cpu == x86_64 ]] && [[ ${_os} == "linux-musl" ]]
    then
      if [[ $_vendor != "unknown" ]]
      then
          cp src/syscfg/lock-obj-pub.x86_64-unknown-linux-musl.h src/syscfg/lock-obj-pub.${_cpu}-${_vendor}-${_os}.h
      fi
    elif [[ $_cpu == arm ]] && [[ ${_os} == linux-musl* ]]
    then
      if [[ $_vendor != "unknown" ]]
      then
        cp src/syscfg/lock-obj-pub.arm-unknown-linux-gnueabi.h src/syscfg/lock-obj-pub.${_cpu}-${_vendor}-${_os}.h
      fi
    fi
  fi
  ${_target}-configure --enable-shared --enable-static .
  make
}

package() {
  cd ${_basename}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  rm "$pkgdir/${_prefix}/${_target}/sysroot/usr/share/info/dir"
}

strip() {
    ${_target}-strip $@
}
export -f strip