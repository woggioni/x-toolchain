pkgname=${_target}-bzip2
pkgver=1.0.8
pkgrel=1
pkgdesc="A high-quality data compression program (${_target})"
arch=(any)
url="http://sources.redhat.com/bzip2"
license=("custom")
makedepends=("${_target}-configure")
depends=("${_target}-gcc")
options=(!buildflags staticlibs)
source=(https://sourceware.org/pub/bzip2/bzip2-$pkgver.tar.gz{,.sig}
        "bzip2-1.0.5-autoconfiscated.patch"
        "bzip2-use-cdecl-calling-convention.patch"
        "mingw32-bzip2-1.0.5-slash.patch")
sha256sums=('ab5a03176ee106d3f0fa90e381da478ddae405918153cca248e682cd0c4a2269'
            'SKIP'
            '7daaca73a46af4636843d1d13bca9cec6e38c0932142a420ad3500098a90db11'
            '8f8f0b65417fe892152e1f0a4f75090d2ef1845d5a611c3c94493704d53452ef'
            '672216b20cf29438ffe43ebf38b9a648d9a0ac6fdc6be55bb4181d57ed5462be')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A')

prepare() {
  cd "$srcdir/bzip2-$pkgver"
  rm -f autogen.sh README.autotools configure.ac Makefile.am bzip2.pc.in
  patch -p1 -i "$srcdir/"bzip2-1.0.5-autoconfiscated.patch
  if [[ "${_os}" == *"mingw"* ]]
  then
    patch bzlib.h < "$srcdir/"bzip2-use-cdecl-calling-convention.patch
    patch -R bzip2.c < "$srcdir/"mingw32-bzip2-1.0.5-slash.patch
  fi
  sh autogen.sh
}

build() {
  cd "$srcdir/bzip2-$pkgver" 
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure
    make
    popd 
}

package() {
    cd "${srcdir}/bzip2-${pkgver}/build-${_target}"
    make DESTDIR="$pkgdir" install
}

strip() {
    ${_target}-strip $@
}
export -f strip