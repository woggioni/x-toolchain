# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 at gmx dot com

pkgbase=nettle
pkgname=${_target}-nettle
pkgver=3.10.1
pkgrel=1
pkgdesc="A low-level cryptographic library"
arch=('x86_64')
url="https://www.lysator.liu.se/~nisse/nettle"
license=('GPL2')
depends=("${_target}-gcc" "${_target}-gmp")
options=(!buildflags staticlibs)
provides=('libnettle.so' 'libhogweed.so')
checkdepends=('valgrind')
source=(https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.gz{,.sig})
sha256sums=('b0fcdd7fc0cdea6e80dcf1dd85ba794af0d5b4a57e26397eee3bc193272d9132'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>


build() {
  cd $pkgbase-$pkgver
  ${_target}-configure .
  make
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir/" install
  rm -r "${pkgdir}/${_prefix}/${_target}/sysroot/usr/"{bin,share}
}

strip() {
    ${_target}-strip $@
}
export -f strip