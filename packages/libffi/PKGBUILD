# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=${_target}-libffi
_basename="${pkgname#${_target}-}"
pkgver=3.4.6
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='http://sourceware.org/libffi/'
license=('MIT')
options=(staticlibs !buildflags)
depends=("${_target}-gcc")
makedepends=("${_target}-configure")
provides=("${_target}-libffi.so")
source=(https://github.com/libffi/libffi/releases/download/v$pkgver/libffi-$pkgver.tar.gz)
sha256sums=('b0dea9df23c863a7a50e825440f3ebffabd65df1497108e5d437747843895a4e')
b2sums=('af8402a09bdbd59b4e9400d2d71bd5ce98f6f1d981d35d1ab40d77a831b13b32c5bd34ca54ff75999e39f0d8a9c066381fae7a8d6c5216d955e064f929f08b88')


build() {
  cd $_basename-$pkgver
  ${_target}-configure --enable-pax_emutramp --disable-exec-static-tramp --disable-multi-os-directory .
  make
}

package() {
  cd $_basename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/${_prefix}/${_target}/sysroot/usr/share/"
}

strip() {
    ${_target}-strip $@
}
export -f strip
