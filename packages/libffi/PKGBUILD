# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=${_target}-libffi
_basename="${pkgname#${_target}-}"
pkgver=3.4.4
pkgrel=2
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='http://sourceware.org/libffi/'
license=('MIT')
options=(staticlibs !buildflags)
depends=("${_target}-gcc")
makedepends=("${_target}-configure")
provides=("${_target}-libffi.so")
source=(https://github.com/libffi/libffi/releases/download/v$pkgver/libffi-$pkgver.tar.gz)
sha256sums=('d66c56ad259a82cf2a9dfc408b32bf5da52371500b84745f7fb8b645712df676')
b2sums=('189fe1ffe9507f204581b0ab09995dc7e7b761bb4eac7e338e9f5ff81431aebcef6c182c1839c9f9acb2706697a260c67e6d1351cf7e2aed7c4eb5d694f6f8fd')

build() {
  cd $_basename-$pkgver
  ${_target}-configure --enable-pax_emutramp .
  make
}

package() {
  cd $_basename-$pkgver
  make DESTDIR="$pkgdir" install
  cp -rlT "$pkgdir"${_prefix}/${_target}/sysroot/usr/lib64 "$pkgdir"${_prefix}/${_target}/sysroot/usr/lib
  rm -r "$pkgdir"${_prefix}/${_target}/sysroot/usr/lib64
  rm -r "$pkgdir/${_prefix}/${_target}/sysroot/usr/share/"
}

strip() {
    ${_target}-strip $@
}
export -f strip
