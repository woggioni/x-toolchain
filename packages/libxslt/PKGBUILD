# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname="${_target}-libxslt"
_basename="${pkgname#${_target}-}"
pkgver=1.1.38
pkgrel=1
pkgdesc="XML stylesheet transformation library"
url="http://xmlsoft.org/XSLT/"
arch=(x86_64)
license=(custom)
options=(staticlibs !buildflags)
depends=("${_target}-libxml2" "${_target}-libgcrypt")
makedepends=(git "${_target}-configure")
checkdepends=(docbook-xml)
_commit=b9196c0b357a903ad0a41cb4acce62cc1b0f2057  # tags/v1.1.38^0
source=(
  "git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=$_commit"
)
b2sums=('SKIP')


pkgver() {
  cd $_basename
  git describe --tags | sed 's/-rc/rc/;s/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $_basename

  # Make xsltproc output reproducible for packages which use it to generate documentation.
  # patch -Np1 -i $srcdir/0002-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_basename
  ${_target}-configure .
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $_basename
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/${_prefix}/${_target}/sysroot/usr/lib"
  mv "$pkgdir/${_prefix}/${_target}/sysroot/lib/python"* "$pkgdir/${_prefix}/${_target}/sysroot/usr/lib"
  rmdir "$pkgdir/${_prefix}/${_target}/sysroot/lib"
}

strip() {
    ${_target}-strip $@
}
export -f strip