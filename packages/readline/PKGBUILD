# Maintainer: pingplug <pingplug@foxmail.com>
# Contributor: Filip Brcic <brcha@gna.org>

# Thanks to the patch from the MXE project <mxe.cc>

pkgname=${_target}-readline
_basever=8.2
_patchlevel=010
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="Library for editing typed command lines (${_target})"
arch=('x86_64')
depends=("${_target}-gcc" "${_target}-ncurses")
makedepends=("${_target}-configure")
provides=("${_target}-libhistory.so" "${_target}-libreadline.so")
options=('staticlibs' '!buildflags' '!emptydirs')
license=('GPL')
url="https://tiswww.case.edu/php/chet/readline/rltop.html"
source=(https://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        inputrc)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [ $_patchlevel -gt 0 ]; then
    for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//.}-$(printf "%03d" $_p){,.sig})
    done
fi

sha256sums=('3feb7171f16a84ee82ca18a36d7b9be109a52c04f492a053331d7d1095007c35'
            'SKIP'
            '36e9611f935ee108d161587b0615f9c390192ef4bbff6dc59b58671261029901'
            'bbf97f1ec40a929edab5aa81998c1e2ef435436c597754916e6a5868f273aff7'
            'SKIP'
            'e06503822c62f7bc0d9f387d4c78c09e0ce56e53872011363c74786c7cd4c053'
            'SKIP'
            '24f587ba46b46ed2b1868ccaf9947504feba154bb8faabd4adaea63ef7e6acb0'
            'SKIP'
            '79572eeaeb82afdc6869d7ad4cba9d4f519b1218070e17fa90bbecd49bd525ac'
            'SKIP'
            '622ba387dae5c185afb4b9b20634804e5f6c1c6e5e87ebee7c35a8f065114c99'
            'SKIP'
            'c7b45ff8c0d24d81482e6e0677e81563d13c74241f7b86c4de00d239bc81f5a1'
            'SKIP'
            '5911a5b980d7900aabdbee483f86dab7056851e6400efb002776a0a4a1bab6f6'
            'SKIP'
            'a177edc9d8c9f82e8c19d0630ab351f3fd1b201d655a1ddb5d51c4cee197b26a'
            'SKIP'
            '3d9885e692e1998523fd5c61f558cecd2aafd67a07bd3bfe1d7ad5a31777a116'
            'SKIP'
            '758e2ec65a0c214cfe6161f5cde3c5af4377c67d820ea01d13de3ca165f67b4c'
            'SKIP')

prepare() {
  cd "${srcdir}/readline-${_basever}"

  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -Np0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done

  if [[ ${_os} == mingw32* ]]
  then
    msg "applying the patch from MXE project"
    patch -Np1 -i ${srcdir}/readline-1.patch
  fi

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  cd "${srcdir}/readline-${_basever}"
    unset LDFLAGS
    mkdir -p build-${_target} && pushd build-${_target}
    if [[ ${_os} == mingw32* ]]
    then
        local CURSES_LIB="-lncursesw"
    else
        local CURSES_LIB="-lncurses"
    fi
    bash_cv_wcwidth_broken=no ${_target}-configure \
      --target=${_target}
    make SHLIB_LIBS="${CURSES_LIB}"
    popd
}

package() {
    cd "${srcdir}/readline-${_basever}/build-${_target}"
        if [[ ${_os} == mingw32* ]]
    then
        local CURSES_LIB="-lncursesw"
    else
        local CURSES_LIB="-lncurses"
    fi
    make install DESTDIR="${pkgdir}" SHLIB_LIBS="${CURSES_LIB}"
    rm "$pkgdir/${_prefix}/${_target}/sysroot/usr/share/info/dir"
}

strip() {
    ${_target}-strip $@
}
export -f strip
