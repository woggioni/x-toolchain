# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Darshit Shah <darnir@gmail.com>

pkgname=${_target}-libpsl
_basename="${pkgname#${_target}-}"
pkgver=0.21.5
pkgrel=1
pkgdesc='Public Suffix List library'
url='https://github.com/rockdaboot/libpsl'
arch=('x86_64')
license=('MIT')
options=('staticlibs' '!buildflags')
depends=(
  ${_target}-gcc
  ${_target}-libidn2
  ${_target}-libunistring
)
makedepends=(
  libxslt
  publicsuffix-list
  python
)
provides=("${_target}-libpsl.so")
source=(https://github.com/rockdaboot/libpsl/releases/download/${pkgver}/${_basename}-${pkgver}.tar.gz{,.sig})
sha512sums=('c14d575cecc0f1693894dd79565b6b9220084ddfa43b908a1cefe16d147cdd5ec47796eb0c2135e2f829a951abaf39d8a371ab5c1352f57b36e610e25adf91f5'
            'SKIP')
b2sums=('a0076f622b85df99f866de6707850ac216b764bdb68c6d516f4603da42dac8eae3ee4c53d68dbb6af6f779c2c7f1b9caab74c8b558209b1f6823f95c13fc3ceb'
        'SKIP')
validpgpkeys=('1CB27DBC98614B2D5841646D08302DB6A2670428') # Tim RÃ¼hsen <tim.ruehsen@gmx.de>

prepare() {
  cd ${_basename}-${pkgver}
  rm -frv list
  autoreconf -fiv
}

build() {
  cd ${_basename}-${pkgver}
  ${_target}-configure \
    --disable-dependency-tracking \
    --disable-gtk-doc \
    --enable-{builtin,runtime}=libidn2 \
    --with-psl-file=/usr/share/publicsuffix/effective_tld_names.dat \
    --with-psl-testfile=/usr/share/publicsuffix/test_psl.txt\
    .
  LC_CTYPE=en_US.UTF-8 make
}


package() {
  depends+=("${_target}-libidn2.so" "${_target}-libunistring.so")

  cd ${_basename}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/${_prefix}/${_target}/sysroot/usr/share/licenses/${_basename}"
  rm -rf "${pkgdir}/${_prefix}/${_target}/sysroot/usr/share/man"
}

strip() {
    ${_target}-strip $@
}
export -f strip
# vim: ts=2 sw=2 et:
