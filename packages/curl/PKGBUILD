# Contributor: ant32 <antreimer@gmail.com>

pkgname="${_target}-curl"
_basename="${pkgname#${_target}-}"
pkgver=8.3.0
pkgrel=1
pkgdesc="An URL retrival utility and library (${_target})"
arch=('x86_64')
url="http://curl.haxx.se"
license=("MIT")

if [[ ${_os} == *"mingw"* ]]
then
  depends=("${_target}-gcc"
          "${_target}-libidn2"
          "${_target}-libssh2"
          "${_target}-openssl"
          "${_target}-zlib"
          "${_target}-libnghttp2")
  _idn_configure_flag="--with-libidn2"
else
  depends=("${_target}-gcc"
          "${_target}-libssh2"
          "${_target}-openssl"
          "${_target}-zlib"
          "${_target}-libnghttp2")
  _idn_configure_flag=""
fi

makedepends=("${_target}-configure")
options=('staticlibs' '!buildflags')
provides=("${_target}-libcurl.so")
source=("https://curl.haxx.se/download/$_basename-$pkgver.tar.gz"{,.asc})
sha512sums=('838f96b6faee949ae70060420d1cc31f3eb018160aaebef53acc850d9cd6d68cf15bf5d76e2838474aff05cb7f51b054bbada09a6257c60ff9892852bdc1da41'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd "${srcdir}"/${pkgname#${_target}-}-${pkgver}
  _configure_args=(
    --with-openssl
    --disable-ldap
    --disable-ldaps
    --disable-manual
    --enable-ipv6
    --enable-threaded-resolver
    ${_idn_configure_flag}
    --with-libssh2
    --enable-versioned-symbols
    --with-gssapi
    --with-ca-bundle="/etc/ssl/certs/ca-certificates.crt"
    --without-random
  )
  mkdir -p build-${_target}-static && pushd build-${_target}-static
  ${_target}-configure ${_configure_args[@]} --enable-static --disable-shared ..
  make
  popd
  mkdir -p build-${_target} && pushd build-${_target}
  ${_target}-configure ${_configure_args[@]} --disable-static --enable-shared ..
  make
  popd
}

package() {
    cd "${srcdir}/${pkgname#${_target}-}-${pkgver}/build-${_target}-static"
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}/${pkgname#${_target}-}-${pkgver}/build-${_target}"
    make DESTDIR="${pkgdir}" install
    sed -i "s/-lidn2/-lidn2 -lunistring/g" "$pkgdir/${_prefix}/${_target}/sysroot/usr/lib/pkgconfig/libcurl.pc"
}

strip() {
    ${_target}-strip $@
}
export -f strip