# Contributor: ant32 <antreimer@gmail.com>

pkgname="${_target}-curl"
_basename="${pkgname#${_target}-}"
pkgver=7.78.0
pkgrel=1
pkgdesc="An URL retrival utility and library (${_target})"
arch=('any')
url="http://curl.haxx.se"
license=("MIT")

if [[ ${_os} == *"mingw"* ]]
then
  depends=("${_target}-gcc"
          "${_target}-libidn2"
          "${_target}-libssh2"
          "${_target}-openssl"
          "${_target}-zlib")
  _idn_configure_flag="--with-libidn2"
else
  depends=("${_target}-gcc"
          "${_target}-libssh2"
          "${_target}-openssl"
          "${_target}-zlib")
  _idn_configure_flag=""
fi

makedepends=("${_target}-configure")
options=('staticlibs' '!buildflags')
provides=("${_target}-libcurl.so")
source=("https://curl.haxx.se/download/$_basename-$pkgver.tar.gz"{,.asc})
sha512sums=('3d74343e06dd3d699f4c883758775554956f7f27de470b71f4af0d7f56ff5e4d7c534ab958c8926fae69cd0ded90c173ac3d5a6ad5518d88c2f39f31f8bba2f3'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd "${srcdir}"/${pkgname#${_target}-}-${pkgver}
    configure_args="--with-openssl \
                    --disable-ldap \
                    --disable-ldaps \
                    --disable-manual \
                    --enable-ipv6 \
                    --enable-threaded-resolver \
                    ${_idn_configure_flag} \
                    --with-libssh2 \
                    --enable-versioned-symbols \
                    --with-gssapi \
                    --without-ca-bundle \
                    --without-random"
    mkdir -p build-${_target}-static && pushd build-${_target}-static
    ${_target}-configure $configure_args \
      --enable-static --disable-shared ..
    make
    popd
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure $configure_args \
      --disable-static --enable-shared ..
    make
    popd
}

package() {
    cd "${srcdir}/${pkgname#${_target}-}-${pkgver}/build-${_target}-static"
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}/${pkgname#${_target}-}-${pkgver}/build-${_target}"
    make DESTDIR="${pkgdir}" install
    sed -i "s/-lidn2/-lidn2 -lunistring/g" "$pkgdir/${_prefix}/${_target}/sysroot/usr/lib/pkgconfig/libcurl.pc"
}

strip() {
    ${_target}-strip $@
}
export -f strip