pkgname=${_target}-lz4
_basename="${pkgname#${_target}-}"
epoch=1
pkgver=1.9.3
pkgrel=2
pkgdesc='Extremely fast compression algorithm'
arch=('x86_64')
url='http://www.lz4.org/'
license=('GPL2')
makedepends=('git')
depends=("${_target}-gcc" "${_target}-cmake")
source=(
    "git+https://github.com/lz4/lz4.git#tag=v$pkgver"
    'https://github.com/lz4/lz4/commit/8301a21773ef61656225e264f4f06ae14462bca7.patch'
)

sha256sums=('SKIP' 'c99da650a79a42db4db23e72146701cce9106383ba45709c5ac89022e36dcfb2')
options=(!buildflags staticlibs)

prepare() {
  cd $_basename
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
    ${_target}-meson ${_basename}/contrib/meson build \
        -Dbin_contrib=false\
        -Dbin_programs=false\
        -Db_lto=true\
        -Ddefault_library=both
    meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

strip() {
    ${_target}-strip $@
}
export -f strip