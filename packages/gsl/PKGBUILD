pkgname=${_target}-gsl
_basename="${pkgname#${_target}-}"
pkgver=2.7.1
pkgrel=1
pkgdesc="The GNU Scientific Library (GSL) is a modern numerical library for C and C++ programmers (${_target})"
arch=('x86_64')
url="http://www.gnu.org/software/gsl/gsl.html"
license=("GPL")
makedepends=("${_target}-configure")
depends=("${_target}-gcc")
options=('staticlibs' '!buildflags')
source=("https://ftp.gnu.org/gnu/gsl/$_basename-$pkgver.tar.gz"{,.sig})
sha256sums=('dcb0fbd43048832b757ff9942691a8dd70026d5da0ff85601e52687f6deeb34b'
            'SKIP')
validpgpkeys=(DD61B7FC02790F978360F399245FB74BAE05B3E9) # Patrick Alken <alken@colorado.edu>

build() {
  cd "$srcdir/gsl-$pkgver"
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure
    make
    popd
}

package() {
    cd "${srcdir}/gsl-${pkgver}/build-${_target}"
    make DESTDIR="$pkgdir" install
}

strip() {
    ${_target}-strip $@
}
export -f strip
