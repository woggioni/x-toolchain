pkgbase=gnutls
pkgname=${_target}-gnutls
pkgver=3.8.1
pkgrel=2
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('x86_64')
license=('GPL3' 'LGPL2.1')
url="https://www.gnutls.org/"
options=(!buildflags staticlibs)
depends=("${_target}-gcc" 
    "${_target}-gmp" 
    "${_target}-libtasn1" 
    "${_target}-readline" 
    "${_target}-zlib" 
    "${_target}-nettle"
    "${_target}-libidn2" 
    "${_target}-zstd" 
    "${_target}-libunistring" 
    "${_target}-brotli")
backup=(etc/gnutls/config
        etc/modules-load.d/gnutls.conf)
source=(https://www.gnupg.org/ftp/gcrypt/gnutls/v3.8/${pkgbase}-${pkgver}.tar.xz{,.sig})
sha256sums=(
    'ba8b9e15ae20aba88f44661978f5b5863494316fe7e722ede9d069fe6294829c'
    'SKIP'
)
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871') # "Daiki Ueno <ueno@unixuser.org>"
#validpgpkeys=('5D46CB0F763405A7053556F47A75A648B3F9220C') # "Zoltan Fridrich <zfridric@redhat.com>"

build() {
  cd ${pkgbase}-${pkgver}
  ${_target}-configure \
  --enable-static \
  --enable-shared \
  --disable-tools \
  --disable-doc \
  --disable-nls \
  --disable-srp-authentication \
	--with-idn \
	--with-brotli \
	--with-zstd \
	--enable-openssl-compatibility \
  --disable-rpath \
  --disable-gtk-doc \
  --disable-full-test-suite \
  --without-p11-kit \
	--disable-ktls \
  --without-tpm \
  --enable-year2038 \
  --without-tpm2 \
  --with-default-trust-store-file=/etc/ssl/certs/ca-certificates.crt \
  --enable-cxx .
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  #rm -r "${pkgdir}/${_prefix}/${_target}/sysroot/usr/"{bin,share}
}

strip() {
    ${_target}-strip $@
}
export -f strip