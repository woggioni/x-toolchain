# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=c-ares
pkgname=${_target}-${pkgbase}
pkgver=1.28.1
pkgrel=1
pkgdesc="A C library for asynchronous DNS requests"
arch=(x86_64)
url="https://c-ares.haxx.se/"
license=(MIT)
options=('staticlibs' '!buildflags')
depends=(${_target}-gcc)
makedepends=(${_target}-cmake ninja)
provides=(${_target}-libcares.so)
source=(https://github.com/$pkgbase/$pkgbase/releases/download/${pkgbase//-}-${pkgver//./_}/$pkgbase-$pkgver.tar.gz{,.asc})
sha512sums=('9a24f40002b661d97c9b49a617d2e6b12bad1c9f22e872611252d051ec650f886c010207939fac6992610b3cd5a87588f802d7dfa1310ced7c6621ea2f99dd90'
            'SKIP')
b2sums=('2d0f12aeb4b947f76fe6c45e2bb0a7401c5d3ae5c84a94f2387f05b639587275e12cb0a2798caa381726e37757973b28c543249f9528b60746b8bf543a70be77'
        'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg <daniel@haxx.se>

build() {
  local cmake_options=(
    -G Ninja
    -B build
    -D CMAKE_BUILD_TYPE=RELEASE
    -DCARES_SHARED=ON
    -DCARES_STATIC=ON
    -S $pkgbase-$pkgver
    -W no-dev
  )
  ${_target}-cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  ln -s ${_prefix}/${_target}/sysroot/usr/lib/libcares_static.a "${pkgdir}/${_prefix}/${_target}/sysroot/usr/lib/libcares.a"
  rm -r "${pkgdir}"/${_prefix}/${_target}/sysroot/usr/share
}

strip() {
    ${_target}-strip $@
}
export -f strip