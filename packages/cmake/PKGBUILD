pkgname="${_target}-cmake"
pkgver=1
pkgrel=18
arch=('any')
pkgdesc="CMake wrapper for x-toolxhain"
depends=('cmake' "${_target}-pkg-config")
license=("GPL")
url="http://fedoraproject.org/wiki/MinGW"
source=("x-cmake.sh"
        "x-toolchain.cmake"
        "mingw_additional_params.cmake")
md5sums=('SKIP' 'SKIP' 'SKIP')

build() {
    local toolchain_files
    local dynamic_linker=$(echo "${_prefix}/${_target}"/lib/ld-*.so*)
    if [[ ${_os} == linux* ]]
    then 
        _os="Linux"
        toolchain_files=('x-toolchain.cmake')
        dynamic_linker=$(echo "${_prefix}/${_target}"/lib/ld-*.so*)
    elif [[ ${_os} == mingw* ]]
    then
        _os="Windows"
        toolchain_files=('x-toolchain.cmake' 'mingw_additional_params.cmake')
    fi  
    for toolchain_file in ${toolchain_files[@]}
    do
        sed -e "s|@TRIPLE@|${_target}|g"\
         -e "s|@PREFIX@|${_prefix}|g"\
         -e "s|@CPU@|${_cpu}|g"\
         -e "s|@OS@|${_os}|g"\
         -e "s|@DYNAMIC_LINKER@|${dynamic_linker}|g"\
         "$toolchain_file" >> toolchain-${_target}.cmake
    done
    sed -e "s|@TRIPLE@|${_target}|g" -e "s|@PREFIX@|${_prefix}|g" x-cmake.sh > ${_target}-cmake
}

package() {
  install -d "${pkgdir}${_prefix}/bin"
  install -d "${pkgdir}${_prefix}/cmake"
  install -m 644 toolchain-${_target}.cmake "${pkgdir}${_prefix}/cmake"
  install -m 755 ${_target}-cmake "${pkgdir}${_prefix}/bin"
}

