# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>
pkgname="${_target}-libjpeg-turbo"
_basename="${pkgname#${_target}-}"
pkgver=3.0.0
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
url='https://libjpeg-turbo.org/'
arch=('x86_64')
license=('custom')
depends=("${_target}-gcc")
makedepends=("${_target}-configure")
options=('staticlibs' '!buildflags')
source=("http://downloads.sourceforge.net/libjpeg-turbo/libjpeg-turbo-$pkgver.tar.gz")
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>
sha512sums=('2307a2e240faf5b3119cdc5cf6a0255813d74591d4a4201fd2374940683cb9de90cb5926367e633f3624b27edd292209c7cd3c38d64522716e83fc81361289b4')

provides=("${_target}-libjpeg=8.2.2")
prepare() {
	cd "${srcdir}/${_basename}-${pkgver}"
	sed 's/Szathm√°ry/Szathmary/g' -i java/org/libjpegturbo/turbojpeg/*.java
}

build() {
	cd "${srcdir}/${_basename}-${pkgver}"
    mkdir -p build-${_target} && pushd build-${_target}
	${_target}-cmake -G Ninja\
		-DCMAKE_INSTALL_INCLUDEDIR=${_prefix}/${_target}/sysroot/usr/include \
		-DWITH_JAVA=0 \
		-DWITH_JPEG8=1 \
		..
	cmake --build .
}

package() {
	cd "${srcdir}/${_basename}-${pkgver}/build-${_target}"
	DESTDIR="${pkgdir}" cmake --install .
	install -m 644 ../jpegint.h "${pkgdir}/${_prefix}/${_target}/sysroot/usr/include" # required by other software
	rm -rf "${pkgdir}/${_prefix}/${_target}/sysroot/"{bin,share}
	rm -rf "${pkgdir}/${_prefix}/${_target}/sysroot/usr/share"
}

strip() {
    ${_target}-strip $@
}
export -f strip