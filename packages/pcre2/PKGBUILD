pkgname=${_target}-pcre2
_basename="${pkgname#${_target}-}"
pkgver=10.42
pkgrel=1
pkgdesc="	A library that implements Perl 5-style regular expressions. 2nd version (${_target})"
arch=('x86_64')
url="http://www.pcre.org/"
license=("BSD")
makedepends=(${_target}-configure ${_target}-readline ${_target}-bzip2 ${_target}-zlib)
depends=("${_target}-gcc")
options=(staticlibs !buildflags)
optdepends=(${_target}-readline ${_target}-bzip2 ${_target}-zlib)

source=("https://github.com/PhilipHazel/pcre2/releases/download/$_basename-$pkgver/$_basename-$pkgver.tar.bz2"{,.sig})
sha512sums=('72fbde87fecec3aa4b47225dd919ea1d55e97f2cbcf02aba26e5a0d3b1ffb58c25a80a9ef069eb99f9cf4e41ba9604ad06a7ec159870e1e875d86820e12256d3'
            'SKIP')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')  # Philip Hazel <ph10@hermes.cam.ac.uk>


build() {
	cd "$srcdir/pcre2-$pkgver"
    mkdir -p build-${_target} && pushd build-${_target}
    if [[ ${_cpu} == s390x ]]
    then
        local JIT_ENABLED="--disable-jit"
    else
        local JIT_ENABLED="--enable-jit"
    fi
    ${_target}-configure \
      ${JIT_ENABLED} \
      --enable-pcre2-16 \
      --enable-pcre2-32 \
      --enable-pcre2grep-libz \
      --enable-pcre2grep-libbz2 \
      --enable-pcre2test-libreadline
    make
    popd
}

package() {
    cd "${srcdir}/pcre2-$pkgver/build-${_target}"
    make DESTDIR="$pkgdir" install
}

strip() {
    ${_target}-strip $@
}
export -f strip