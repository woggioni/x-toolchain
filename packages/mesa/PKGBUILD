pkgname=${_target}-mesa
pkgver=23.2.1
pkgrel=1
pkgdesc="An open-source implementation of the OpenGL specification (${_target})"
arch=("x86_64")
url="https://www.mesa3d.org/"
license=("custom")
options=('!buildflags' 'staticlibs' '!emptydirs')
makedepends=("${_target}-meson" "${_target}-cmake" "python-mako" "glslang")
depends=(
  "${_target}-vulkan-icd-loader" 
  "${_target}-llvm" 
  #"${_target}-dlfcn"
)
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D'  # Emil Velikov <emil.l.velikov@gmail.com>
              '946D09B5E4C9845E63075FF1D961C596A7203456'  # Andres Gomez <tanty@igalia.com>
              'E3E8F480C52ADD73B278EE78E1ECBE07D7D70895'  # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>
              'A5CC9FEC93F2F837CB044912336909B6B25FADFA'  # Juan A. Suarez Romero <jasuarez@igalia.com>
              '71C4B75620BC75708B4BDB254C95FAAB3EB073EC'  # Dylan Baker <dylan@pnwbakers.com>
              '57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>
source=(
  https://mesa.freedesktop.org/archive/mesa-${pkgver}.tar.xz{,.sig}
)
sha256sums=('64de0616fc2d801f929ab1ac2a4f16b3e2783c4309a724c8a259b20df8bbc1cc'
            'SKIP' 
)

prepare () {
  cd "${srcdir}"/mesa-${pkgver}
}

build() {
  local meson_options=(
    --warnlevel=0
    -Ddefault_library=both
    -Dplatforms=wayland 
    -Dgallium-drivers=zink
    -Dvulkan-drivers=amd,intel 
    -Degl=disabled
    -Dopengl=true
    -Dllvm=enabled
    -Dshared-llvm=enabled
    -Dglx=disabled
    -Dshared-glapi=enabled
    -Dgles1=enabled 
    -Dgles2=enabled 
    -Dosmesa=false 
    -Dvulkan-icd-dir=bin 
    --includedir=include/mesa
  )
  ${_target}-meson ${meson_options[@]} "${srcdir}"/mesa-${pkgver} build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm "$pkgdir/${_prefix}/${_target}/sysroot/usr/lib/libGLESv2.so"*
  rm "$pkgdir/${_prefix}/${_target}/sysroot/usr/lib/pkgconfig/glesv2.pc"
}

strip() {
    ${_target}-strip $@
}

export -f strip