pkgname="${_target}-libunistring"
_basename="${pkgname#${_target}-}"
pkgver=1.2
pkgrel=1
pkgdesc="Library for manipulating Unicode strings and C strings. (${_target})"
arch=('x86_64')
url="http://www.gnu.org/software/libunistring/"
license=("GPL")
makedepends=("${_target}-configure")
depends=("${_target}-gcc")
options=(staticlibs !buildflags)
provides=("${_target}-libunistring.so")
source=(https://ftp.gnu.org/gnu/$_basename/${_basename}-${pkgver}.tar.xz{,.sig})
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871'  # Daiki Ueno <ueno@unixuser.org>
              '9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D') # Bruno Haible (Open Source Development) <bruno@clisp.org>
sha256sums=('632bd65ed74a881ca8a0309a1001c428bd1cbd5cd7ddbf8cedcd2e65f4dcdc44'
            'SKIP')
b2sums=('606c6fdd93f05b01e9b08b3bd30283afcbf9a3425abf900e7672f1d6eb91b51009913d4f5e45a4c1c1b82a84b4870d3eaced9ca76fd570936ad5c39c94038fc7'
        'SKIP')


build() {
	cd "${srcdir}/libunistring-${pkgver}"
    unset LDFLAGS
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure
    make
    popd
}

package() {
    cd "${srcdir}/libunistring-${pkgver}/build-${_target}"
    make DESTDIR="$pkgdir" install
    rm -f "$pkgdir/${_prefix}/${_target}/sysroot/lib/charset.alias"
    rm -rf "$pkgdir/${_prefix}/${_target}/sysroot/share"
    rm -r "$pkgdir/${_prefix}/${_target}/sysroot/usr/share"
}

strip() {
    ${_target}-strip $@
}
export -f strip