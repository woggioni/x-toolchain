# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: congyiwu <congyiwu AT gmail DOT com>

pkgname="${_target}-json-c"
_basename="${pkgname#${_target}-}"
pkgver=0.17
pkgrel=1
pkgdesc="A JSON implementation in C"
url="https://github.com/json-c/json-c/wiki"
license=(MIT)
arch=('x86_64')
depends=("${_target}-gcc")
options=('staticlibs' '!buildflags')
makedepends=(git "${_target}-cmake")
provides=("${_target}-libjson-c.so")
_commit=b4c371fa0cbc4dcbaccc359ce9e957a22988fb34  # tags/json-c-0.17-20230812^0
source=("git+https://github.com/json-c/json-c#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd json-c
  local tag="$(git describe --tags --abbrev=0)"
  local ver="$(git describe --tags)"
  echo "${tag%-*}${ver#$tag}" | sed 's/^json-c-//;s/-/+/g'
}

build() {
  CFLAGS+=" $CPPFLAGS"
  CXXFLAGS+=" $CPPFLAGS"

  "${_target}-cmake" -G Ninja -Hjson-c -Bbuild \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=${_prefix}/${_target}/sysroot/usr/ \
    -DCMAKE_INSTALL_LIBDIR=${_prefix}/${_target}/sysroot/usr/lib \
    -DENABLE_THREADING=ON \
    -DENABLE_RDRAND=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
}

strip() {
    ${_target}-strip $@
}
export -f strip
