# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# after a .so bump first rebuild dirmngr
# with sudo testing-x86_64-build -- -I libgcrypt-1.6.0-1-x86_64.pkg.tar.xz
# then cp /usr/lib/libgcrypt.so.11 /var/lib/archbuild/staging-x86_64/root/usr/lib/ and do staging-x86_64-build

pkgname=${_target}-libgcrypt
_basename="${pkgname#${_target}-}"
pkgver=1.10.2
pkgrel=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=(x86_64)
url="http://www.gnupg.org"
license=('LGPL')
options=(staticlibs !buildflags)
depends=("${_target}-libgpg-error")
makedepends=("${_target}-configure")
source=("https://gnupg.org/ftp/gcrypt/libgcrypt/libgcrypt-${pkgver}.tar.bz2"{,.sig}
        "libgcrypt-use-correct-def-file.patch"
        "Smarter-fig2dev-detection.all.patch")
sha1sums=('0b9555960d84a09ea14e52360808f2e02e9c12d2'
          'SKIP'
          '0f03d59dee4b967dfa80621ef4b1efea61772c16'
          '3613a5454aeaef8d264011f2bc9f2303248fb933')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # Werner Koch

prepare() {
  cd ${_basename}-${pkgver}
  patch -p1 -i "$srcdir"/libgcrypt-use-correct-def-file.patch
  patch -p1 -i "${srcdir}"/Smarter-fig2dev-detection.all.patch
  autoreconf -fi 
}

build() {
  mkdir build
  cd build
  ${_target}-configure \
    --with-gpg-error-prefix="${_prefix}/${_target}/sysroot/" \
    --enable-static \
    --enable-shared \
    --disable-asm \
	  --disable-padlock-support "${srcdir}/${_basename}-${pkgver}"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  rm "$pkgdir/${_prefix}/${_target}/sysroot/usr/share/info/dir"
}

strip() {
    ${_target}-strip $@
}
export -f strip
