# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# after a .so bump first rebuild dirmngr
# with sudo testing-x86_64-build -- -I libgcrypt-1.6.0-1-x86_64.pkg.tar.xz
# then cp /usr/lib/libgcrypt.so.11 /var/lib/archbuild/staging-x86_64/root/usr/lib/ and do staging-x86_64-build

pkgname=${_target}-libgcrypt
_basename="${pkgname#${_target}-}"
pkgver=1.11.0
pkgrel=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=(x86_64)
url="http://www.gnupg.org"
license=('LGPL')
options=(staticlibs !buildflags)
depends=("${_target}-libgpg-error")
makedepends=("${_target}-configure")
# https://www.gnupg.org/download/integrity_check.html
source=(https://gnupg.org/ftp/gcrypt/${_basename}/${_basename}-${pkgver}.tar.bz2{,.sig})
sha256sums=('09120c9867ce7f2081d6aaa1775386b98c2f2f246135761aae47d81f58685b9c'
            'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # Niibe Yutaka (GnuPG Release Key)

prepare() {
  cd ${_basename}-${pkgver}
  # tests fail due to systemd+libseccomp preventing memory syscalls when building in chroots
  #  t-secmem: line 176: gcry_control (GCRYCTL_INIT_SECMEM, pool_size, 0) failed: General error
  #  FAIL: t-secmem
  #  t-sexp: line 1174: gcry_control (GCRYCTL_INIT_SECMEM, 16384, 0) failed: General error
  #  FAIL: t-sexp
  sed -i "s:t-secmem::" tests/Makefile.am
  sed -i "s:t-sexp::" tests/Makefile.am
  autoreconf -vfi
}

build() {
  mkdir build
  cd build
  ${_target}-configure \
    --with-gpg-error-prefix="${_prefix}/${_target}/sysroot/" \
    --enable-static \
    --enable-shared \
    --disable-asm \
	  --disable-padlock-support "${srcdir}/${_basename}-${pkgver}"
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
  rm "$pkgdir/${_prefix}/${_target}/sysroot/usr/share/info/dir"
}

strip() {
    ${_target}-strip $@
}
export -f strip
