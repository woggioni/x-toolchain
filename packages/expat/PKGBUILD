pkgname=${_target}-expat
_basename="${pkgname#${_target}-}"
pkgver=2.6.2
pkgrel=1
pkgdesc="An XML parser library (${_target})"
arch=('x86_64')
url="http://expat.sourceforge.net"
license=("custom")
makedepends=("${_target}-configure" "${_target}-cmake" 'ninja')
depends=("${_target}-gcc")
options=(!buildflags staticlibs)
source=(https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/$_basename-$pkgver.tar.bz2{,.asc})
sha512sums=('15811413e92a632272188781cc3f2a9e52ed62f6edfad9b2eeeca0946e53132b6c9ca6dc460eda766d6a4e68e5920128335d705f9556b5aa3f77593658780470'
            'SKIP')
b2sums=('9b518cf768e53b177fd933d37686a76c4a1bd1e2829cf414f8f9c054901f61d6445232eb4d4502e888507efa12a48d8ace206fbabac5d2ca5365f0437e971fd8'
        'SKIP')
validpgpkeys=(3176EF7DB2367F1FCA4F306B1F9B0E909AF37285) # Sebastian Pipping

prepare() {
    local cmake_options=(
        -G Ninja
        -D CMAKE_BUILD_TYPE=Release
        -D EXPAT_BUILD_EXAMPLES=OFF
        -D EXPAT_BUILD_TESTS=OFF
        -S "expat-${pkgver}"
        -W no-dev
   )
   "${_target}-cmake" -DEXPAT_SHARED_LIBS=OFF -B build-static "${cmake_options[@]}" 
   "${_target}-cmake" -DEXPAT_SHARED_LIBS=ON -B build-shared "${cmake_options[@]}" 
}

build() {
    cmake --build build-static
    cmake --build build-shared
}

package() {
    DESTDIR="$pkgdir" cmake --install build-shared
    install -vDm 644 build-static/libexpat.a -t "$pkgdir/${_prefix}/${_target}/sysroot/usr/lib/"
}

strip() {
    ${_target}-strip $@
}
export -f strip